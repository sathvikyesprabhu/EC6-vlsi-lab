* nmos characteristics
.include ./t14y_tsmc_025_level3.txt

* NETLIST
vd out d dc 0
m1 d g 0 0 cmosn l=100u w=0.8u
vcap out cap dc 0
cl cap 0 0.5p
v_dd vdd 0 dc 5
vg g 0 dc 5 pulse(0 5 0 0.1u 0.1u 10u 20u)

m2 d g vdd vdd cmosp l=100u w=4u


.control
  * Vary c load
  foreach cl_new 0.5p 1p 2p 5p
    dc vg 0 5 0.01
    alter cl=$cl_new
    run
        
    * DC measurements
    meas dc vol min d
    meas dc voh max d
    let v90 = voh - 0.1 * (voh-vol)
    let v10 = vol + 0.1 * (voh-vol)
    let v50 = voh - 0.5 * (voh-vol)
    
    * transient response
    tran 0.1u 40u
    
    * transient measurements
    meas tran trise trig d val=dc.v10 rise=1 targ d val=dc.v90 rise=1
    meas tran tfall trig d val=dc.v90 fall=1 targ d val=dc.v10 fall=1
    meas tran tphl trig g val=dc.v50 rise=1 targ d val=dc.v50 fall=1
    meas tran tplh trig g val=dc.v50 fall=1 targ d val=dc.v50 rise=1
    let td = 0.5 * (tphl + tplh)
    print td
    let power = -5 * (v_dd#branch)
    meas tran pavg AVG power from=0n to=40u
  end
  plot (tran1.d) (tran2.d) (tran3.d) (tran4.d) (g)
.endc
