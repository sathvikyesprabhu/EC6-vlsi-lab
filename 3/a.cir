* nmos characteristics
.include ./t14y_tsmc_025_level3.txt

* NETLIST
vd out d 0
m1 d g 0 0 cmosn l=2.5u w=10u
vcap out cap dc 0
cl cap 0 0.5p
v_dd vdd 0 5
vg g 0 pulse(0 5 0 1n 1n 10n 20n)

m2 vdd vdd d 0 cmosn l=2.5u w=10u

.control
  * Vary w of driver
  foreach w_new 1u 40u 80u
    dc vg 0 5 0.01
    alter m1 w=$w_new
    run
  
    * DC measurements
    meas dc vol min d
    meas dc voh max d
    let v90 = voh - 0.1 * (voh-vol)
    let v10 = vol + 0.1 * (voh-vol)
    let v50 = voh - 0.5 * (voh-vol)
    let slope = deriv(d)
    meas dc vil find g when slope=-1 cross=1
    meas dc vih find g when slope=-1 cross=2
    let nmh = voh-vih
    let nml = vil-vol
    print nmh nml
    
    * transient response
    tran 0.1n 40n
    
    * transient measurements
    meas tran trise trig d val=dc.v10 rise=1 targ d val=dc.v90 rise=1
    meas tran tfall trig d val=dc.v90 fall=1 targ d val=dc.v10 fall=1
    meas tran tphl trig g val=dc.v50 rise=1 targ d val=dc.v50 fall=1
    meas tran tplh trig g val=dc.v50 fall=1 targ d val=dc.v50 rise=1
    let td = 0.5 * (tphl + tplh)
    print td
    let power = -5 * (v_dd#branch)
    meas tran pavg AVG power from=0n to=40n
  end
  plot (dc1.d) (dc2.d) (dc3.d)
  plot (tran1.d) (tran2.d) (tran3.d) (g)
  plot (-5*tran1.v_dd#branch) (-5*tran2.v_dd#branch) (-5*tran3.v_dd#branch)
  
  * Vary l of driver
  foreach l_new 5u 1.25u 0.3125u 
    dc vg 0 5 0.01
    alter m1 l=$l_new
    run
    
    * DC measurements
    meas dc vol min d
    meas dc voh max d
    let v90 = voh - 0.1 * (voh-vol)
    let v10 = vol + 0.1 * (voh-vol)
    let v50 = voh - 0.5 * (voh-vol)
    let slope = deriv(d)
    meas dc vil find g when slope=-1 cross=1
    meas dc vih find g when slope=-1 cross=2
    let nmh = voh-vih
    let nml = vil-vol
    print nmh nml
    
    * transient response
    tran 0.1n 40n
    
    * transient measurements
    meas tran trise trig d val=dc.v10 rise=1 targ d val=dc.v90 rise=1
    meas tran tfall trig d val=dc.v90 fall=1 targ d val=dc.v10 fall=1
    meas tran tphl trig g val=dc.v50 rise=1 targ d val=dc.v50 fall=1
    meas tran tplh trig g val=dc.v50 fall=1 targ d val=dc.v50 rise=1
    let td = 0.5 * (tphl + tplh)
    print td
    let power = -5 * (v_dd#branch)
    meas tran pavg AVG power from=0n to=40n
  end
  plot (dc4.d) (dc5.d) (dc6.d)
  plot (tran4.d) (tran5.d) (tran6.d) (g)
  plot (-5*tran4.v_dd#branch) (-5*tran5.v_dd#branch) (-5*tran6.v_dd#branch)
  
.endc
