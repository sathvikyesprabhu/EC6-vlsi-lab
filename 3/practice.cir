*nmos characteristics
.include ./t14y_tsmc_025_level3.txt

m1 d g 0 0 cmosn l=1u w=10u
*r1 vdd d 10k 
m2 vdd vdd d 0 cmosn l=1u w=0.1u
c1 d 0 0.1p

v_in g 0 pulse(0 5 0 1p 1p 50n 100n)
v_dd vdd 0 5

.control

foreach wid 0.5u 1u 2u 
 dc v_in 0 5 0.1
 alter m1 w=$wid
 
*nm calculations
meas dc vol MIN d
meas dc voh MAX d

let v90= vol + 0.9*(voh-vol)
let v50= vol + 0.5*(voh-vol)
let v10= vol + 0.1*(voh-vol)
let slope=deriv(d)
meas dc vil find g when slope=-1 cross=1
meas dc vih find g when slope=-1 cross=2
let nmh=voh-vih
let nml=vil-vol
print nmh nml

*transient analysis
tran 1n 400n

*transient measurements
meas tran trise TRIG d val=dc.v10 rise=1 targ d val=dc.v90 rise=1
meas tran tfall TRIG d val=dc.v90 fall=1 targ d val=dc.v10 fall=1
meas tran tplh TRIG g val=dc.v50 fall=1 targ d val=dc.v50 rise=1
meas tran tphl TRIG g val=dc.v50 rise=1 targ d val=dc.v50 fall=1
let td = 0.5*(tplh+tphl)
print td
let power = -vdd*v_dd#branch
meas tran pavg avg power from=1n to=400n


end

plot (dc1.d) (dc2.d) (dc3.d)
plot (tran1.d) (tran2.d) (tran3.d) (g)
plot (-5*tran1.v_dd#branch) (-5*tran2.v_dd#branch) (-5*tran3.v_dd#branch)
.endc
